*+AX_BARPRE	Computes matrix of general precession from epochs DEQ1 to DEQ2
	SUBROUTINE AX_BARPRE(DEQ1,DEQ2,DPREMA)
	IMPLICIT DOUBLE PRECISION (D)
	DIMENSION DPREMA(3,3)
*DEQ1	Input	First epoch, years.
*DEQ2	Input	Second epoch, years.
*DPREMA	Output	Precession matrix.
*-
*Author P.Stumpff, Astron & Astophys 1979 July 15.
*    THE PRECESSION ANGLES (DZETA,DZETT,DTHET) ARE COMPUTED FROM THE
*    constants (dc1-dc9) corresponding to the definitions in the
*    explanatory supplement to the astronomical emphemeris (1961,p30f).
*
	PARAMETER (DCSAR = 4.848136812D-6,DC1900 = 1900.0D0,DC1M2 = 0.01D0,
     &     DC1 = 2304.25D0,DC2 = 1.396D0,DC3 = 0.302D0,DC4 = 0.018D0,
     &     DC5 = 0.791D0,DC6 = 2004.683D0,DC7 = -0.853D0,DC8 = -0.426D0,
     &     DC9 = -0.042D0)
	DT0 = (DEQ1-DC1900)*DC1M2
	DT  = (DEQ2-DEQ1)*DC1M2
	DTS = DT*DT
	DTC = DTS*DT
	DZETA = ((DC1+DC2*DT0)*DT+DC3*DTS+DC4*DTC)*DCSAR
	DZETT = DZETA + DC5*DTS*DCSAR
	DTHET = ((DC6+DC7*DT0)*DT+DC8*DTS+DC9*DTC)*DCSAR
	DSZETA = SIN(DZETA)
	DCZETA = COS(DZETA)
	DSZETT = SIN(DZETT)
	DCZETT = COS(DZETT)
	DSTHET = SIN(DTHET)
	DCTHET = COS(DTHET)
	DA = DSZETA*DSZETT
	DB = DCZETA*DSZETT
	DC = DSZETA*DCZETT
	DD = DCZETA*DCZETT
	DPREMA(1,1) = DD*DCTHET - DA
	DPREMA(1,2) = -DC*DCTHET - DB
	DPREMA(1,3) = -DSTHET*DCZETT
	DPREMA(2,1) = DB*DCTHET + DC
	DPREMA(2,2) = -DA*DCTHET + DD
	DPREMA(2,3) = -DSTHET*DSZETT
	DPREMA(3,1) = DCZETA*DSTHET
	DPREMA(3,2) = -DSZETA*DSTHET
	DPREMA(3,3) = DCTHET
	END
