*+AX_DONVRT	Converts spherical coordinates from one system to another.
	SUBROUTINE AX_DONVRT(RA,DEC,CTOE,AZ,EL)
	DOUBLE PRECISION RA, DEC, CTOE(3,3), AZ, EL
*RA	Input	Original RA/longitude coordinate, radians.
*DEC	Input	Original dec/latitude coordinate, radians.
*CTOE	Input	Conversion matrix generated by CALL AX_DONGEN etc.
*AZ	Output	Azimuth/longitude coordinate, radians (0 to 2*PI)
*EL	Output	Elevation/latitude coordinate, radians.
*-
*Author: Gavin Eadie, 1972;  Fortran-77 version by Clive Page, 1984 November.
	DOUBLE PRECISION VIN(3),VOUT(3)
	CALL AX_DONA2V(RA,DEC,VIN)
	DO J = 1,3
	  VOUT(J) = VIN(1)*CTOE(1,J)+VIN(2)*CTOE(2,J)+VIN(3)*CTOE(3,J)
	END DO
	CALL AX_DONV2A(VOUT,AZ,EL)
	END
