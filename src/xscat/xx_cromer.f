*+XX_CROMER	Atomic scattering factors, Cromer and Liberman
	SUBROUTINE XX_CROMER(IN,IO,XRAY,THETA,FO,SUMFP,SUMFPP,ABSO)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	INTEGER IN,IO
	DOUBLE PRECISION XRAY,THETA,FO,SUMFP,SUMFPP,ABSO
*IN	input	channel for x-section data
*IO	output 	hannel for diagnostics (=0 for no diagnostics)
*XRAY	input	x-ray wavelength in Angstoms
*THETA	input	half angle between incident and scattered photon (Bragg angle)
*FO	output	atomic scattering factor
*SUMFP	output	real part of anomalous scattering factor
*SUMFPP	output	imaginary part of anomalous scattering factor
*ABSO	output	mass absorption coefficient cm**2/gm
*-Author Dick Willingale way back when, after original by Cromer and Liberman
C
C     ROUTINE TO INTERPOLATE CROSS SECTIONS AND COMPUTE
C     ANOMALOUS DISPERSION FACTORS, ADAPTED FROM LASL
C     REPORT LA-4403 BY D.T.CROMER AND D.LIBERMANN, MAY 1970.
C     INCLUDES AN ANALYTIC WEIGTHED FIT CALCULATION OF ATOMIC
C     SCATTERING FACTOR FO. REFERENCE D.T.CROMER AND J.T.WABER
C     ACTA CRYST.(1965).18,104
C
C     PARAMETERS EXPLAINED IN GLOSSARY BELOW
C
C            INPUT DATA REQUIRED
C     TAPE IN HOLDS CROSS SECTION DATA ETC.
C     CARD 1 SYMBOL OF ATOM AND NUMBER OF ORBITALS, ATOMIC WEIGHT
C     AND ENERGY TERM FROM TABLE 2 OF REPORT TEXT.
C     ALSO ATOMIC NUMBER READ IN FOR USE OUTSIDE CROMER
C     FORMAT(A2,I3,3F10.5)
C     CARD 2 ORBITAL, FUNCTION TYPE AND BINDING ENERGY.
C     FORMAT(A6,I4,1P2E15.4)
C     CARD 3 THROUGH 12 ENERGY AND CROSS SECTION. FORMAT(1P2E15.4)
C     ENERGIES MUST BE IN INCREASING ORDER.
C     CARDS OF TYPE 2 AND 3 REPEATED FOR ALL ORBITALS SPECIFIED
C     BY CARD 2.
C     FINAL CARD HOLDS COEFFS. FOR FO CALCULATION FORMAT(9F8.4)
C
C           GLOSSARY
C     ABSO=MASS ABSORPTION COEFF. IN CM**2/GM
C     ANU=ATOMIC NUMBER (NOT USED WITHIN CROMER)
C     ATOM=ATOMIC SYMBOL
C     ATWT=ATOMIC WEIGHT
C     AO=FACTOR TO CONVERT BARNS TO ATOMIC UNITS
C     BB=BINDING ENERGY IN ATOMIC UNITS
C     BE=BINDING ENERGY IN KEV
C     C=VELOCITY OF LIGHT IN ATOMIC UNITS
C     CORR=CORRECTION FOR DISCONTINUITY IN INTEGRAL (SEE TEXT)
C     CWRK=B-SPLINE ARRAY USED IN INTERPOLATION
C     CX=INTERPOLATED CROSS SECTIONS IN ATOMIC UNITS
C     CXINT=LOG(CX) FOUND BY INTERPOLATION
C     CXB=TOTAL CROSS SECTION IN BARNS
C     C1=FACTOR TO CONVERT KEV TO ATOMIC UNITS
C     E(10)=ENERGIES AT WHICH CROSS SECTIONS WERE CALCULATED
C     EG1=SMALLEST ENERGY USED BY GAUSSIAN-LEGENORE QUADRATURE
C     ES(10)=LOG OF ENERGIES IN KEV USED FOR INTERPOLATION
C     ETERM=ENERGY TERM FROM TABLE 2 OF REPORT TEXT
C     E01BAF,E02BBF=NAG ROUTINES PERFORMING INTERPOLATION
C     BY FITTING A CUBIC SPLINE TO DATA
C     FO=ATOMIC SCATTERING FACTOR
C     FP=ORBITAL CONTRIBUTION TO DELTA F PRIME
C     FPP=ORBITAL CONTRIBUTION TO DELTA F DOUBLE PRIME
C     ICOUNT=INDEX USED IN SIGMA FUNCTIONS
C     IF=FUNCTION TYPE SPECIFIED BY BE
C     IN=FILE HOLDING CROSS SECTION DATA
C     IO=FILE FOR OUTPUT (FORMATED FOR LINE PRINTER)
C     J=INDEX
C     K=INDEX
C     KWRK=KNOTS OF SPLINE USED IN INTERPOLATION
C     L=INDEX
C     LGNDR=FUNCTION TO COMPUTE WEIGHTS AND ABSCISSAE FOR GAUSS
C     M=INDEX
C     NO=NUMBER OF ORBITALS
C     RX=WAVELENGTHS IN ATOMIC UNITS
C     S(10)=CALCULATED CROSS SECTIONS AT ENERGIES E(10)
C     SS(10)=LOG OF CROSS SECTIONS IN BARNS USED IN INTERPOLATION
C     SG(5)=CROSS SECTIONS CALCULATED AT EG(5)
C     SHELL=NAME OF ORBITAL
C     SIGMA0=FUNCTION FOR IF=0
C     SIGMA1=FUNCTION FOR IF=1
C     SIGMA2=FUNCTION FOR IF=2
C     SUMFP=DELTA F PRIME
C     SUMFPP=DELTA F DOUBLE PRIME
C     SX=SIN(THETA)/XRAY
C
C     AND SCATTERED PHOTON)
C     W=WORKING ARRAY USED IN INTERPOLATION
C     XL=LOG(XR)
C     XR=WAVELENGTH IN KEV
C     XRAY=WAVELENGTH IN ANGSTROMS OF INCIDENT PHOTON
C
C            NOTES
C     THE NAG LIBRARY MUST BE LINKED TO THIS JOB
C     IF CX IS FOUND BY EXTRAPOLATION WHEN THE INCIDENT ENERGY
C     IS JUST ABOVE AN ABSORPTION EDGE "*" IS PRINTED NEXT
C     TO THE FPP VALUE
C     IF IO=0 NO LISTING FILE IS PRODUCED
C
      DOUBLE PRECISION KWRK(15),DIF
      DOUBLE PRECISION E(10),S(10),ES(10),SS(10),XRA(5),W(76),CWRK(15)
	CHARACTER ATOM*2
      COMMON/CONST/PI,PI2,CON1,CONV
      COMMON/GAUS/CX,BB,SG(5),RX,ICOUNT
      COMMON/ATOMIC/ATWT,TOM,ANU
      EXTERNAL XX_SIGMA0,XX_SIGMA1,XX_SIGMA2,XX_LGNDR
1     FORMAT(F10.5,A10,F10.5)
2     FORMAT(A2,I3,3F10.5)
3     FORMAT(A6,I4,E15.4)
4     FORMAT(2E15.4)
5     FORMAT(9F8.4)
6     FORMAT('0',2X,'ATOMIC SCATTERING FACTOR FOR ',
     +A2,' SIN(THETA)/LAMBDA= ',F7.4, ' 1/A,',2X,'F0= ',F6.2)
7     FORMAT('0',2X,'ANOMALOUS SCATTERING FACTORS FOR ',A2,' AT',
     +F10.5,'A',//28X,'FP',5X,'FPP')
10    FORMAT(15X,A6,3X,2F7.3,'  *')
11    FORMAT(15X,A6,3X,2F7.3)
12    FORMAT('0',15X,'TOTAL',3X,2F7.3)
13    FORMAT('0',22X,'MU/RHO=',F12.2,' CM**2/GM')
	C=137.0367
        AO=2.80022E+7
      	C1=.02721
	XRA(1)=5.4147E0
	XRA(2)=6.4038E0
	XRA(3)=8.0477E0
	XRA(4)=1.7480E1
	XRA(5)=2.2164E1
	PI=3.14159265359
	PI2=2.D0*PI
	CON1=2.81785E-5
	CONV=180.D0/PI
      XR=12.397639/XRAY
      RX=XR/C1
      READ(IN,2) ATOM,NO,ATWT,ETERM,ANU
      IF(IO.EQ.0) GOTO 110
      WRITE(IO,7) ATOM,XRAY
110   CONTINUE
      SUMFP=ETERM
      SUMFPP=0.
      CXB=0.
      DO 500 L=1,NO
      CX=0.
      READ(IN,3) SHELL,IF,BE
130   BB=BE/C1
      READ(IN,4) (E(J),S(J),J=1,10)
      K=0
      M=0
      DO 150 J=1,10
      DO 140 I=1,5
      	DIF=ABS((E(J)-XRA(I))/XRA(I))
      	IF(DIF.LT.1.E-5) GOTO 150
140   CONTINUE
      M=M+1
      IF(M.EQ.1) EG1=E(J)
C STORE CROSS SECTIONS FOR GAUSS
      SG(M)=S(J)/AO
150   CONTINUE
      IF(BE.GT.XR) GOTO 370
      DO 300 J=1,10
      IF(S(J).NE.0.) GOTO 260
      GOTO 300
260   K=K+1
C STORE ENERGIES AND CROSS SECTIONS FOR INTERPOLATION
      ES(K)=DLOG(E(J))
      SS(K)=DLOG(S(J))
300   CONTINUE
      XL=DLOG(XR)
C     INTERPOLATE TO FIND CROSS SECTION OF LTH ORBITAL AT ENERGY XR
	ki=k
	j=ki
	do k=2,j
		if(xl.le.es(k)) then
			ki=k
			goto 301
		endif
	enddo	
301	continue		
	IF(XL.LT.DLOG(BE)) THEN
		CXINT=-1000.
	ELSE
		KI1=KI-1
		CXINT=SS(KI1)+(XL-ES(KI1))*(SS(KI1)-SS(KI))/(ES(KI1)-ES(KI))
		CXINT=MAX(CXINT,-1000.D0)
	ENDIF
	if(CXINT.EQ.-1000.0) THEN
		CX=0.0
	else
      		CX=EXP(CXINT)
	endif
      CXB=CXB+CX
      CX=CX/AO
370   ICOUNT=6
C INTEGRATE TO FIND FP FOR LTH ORBITAL
      IF(IF.EQ.0) FP=XX_GAUSS(XX_SIGMA0,5,XX_LGNDR)*C/(2.*PI**2)
      IF(IF.EQ.1) FP=XX_GAUSS(XX_SIGMA1,5,XX_LGNDR)*C/(2.*PI**2)
      IF(IF.EQ.2) FP=XX_GAUSS(XX_SIGMA2,5,XX_LGNDR)*C/(2.*PI**2)
      FPP=0.
      IF(CX.NE.0.) FPP=C*CX*RX/(4.*PI)
      CORR=0.
      IF(CX.NE.0.) CORR=-CX*RX*0.5*LOG((RX+BB)/(RX-BB))*C/(2.*PI**2)
      FP=FP+CORR
      SUMFP=SUMFP+FP
      SUMFPP=SUMFPP+FPP
      IF(IO.EQ.0) GOTO 500
      IF((XR.GE.BE).AND.(XR.LT.EG1)) THEN
		WRITE(IO,10) SHELL,FP,FPP
	ELSE
		WRITE(IO,11) SHELL,FP,FPP
	ENDIF
500   CONTINUE
      IF(IO.EQ.0) GO TO 550
      WRITE(IO,12) SUMFP,SUMFPP
550   CONTINUE
      ABSO=CXB*.602472/ATWT
      IF(IO.EQ.0) GOTO 560
      WRITE(IO,13) ABSO
560   CONTINUE
C READ COEFFS. FOR CALCULATION OF ATOMIC SCATTERING FACTOR
      READ(IN,5) (SS(J),J=1,9)
      SX=DSIN(THETA)/XRAY
      FO=0.
      I=1
600   II=I+1
      FO=FO+SS(I)*DEXP(-SS(II)*SX**2)
      I=I+2
      IF(I.LE.8) GOTO 600
      FO=FO+SS(9)
      IF(IO.EQ.0) RETURN
      WRITE(IO,6) ATOM,SX,FO
      RETURN
      END
